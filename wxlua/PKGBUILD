# Maintainer: Albert Kugel (Lastebil) (lastebilaur@trebel.org)
# Contributor : speps <speps at aur dot archlinux dot org>
# Contributor: Eric Forgeot < http://ifiction.free.fr >
# Submitter: favardin

pkgname=wxlua
pkgver=2.8.12.3
pkgrel=2
pkgdesc="A set of bindings to the wxWidgets library for the Lua programming language."
arch=('i686' 'x86_64')
url="http://wxlua.sourceforge.net"
license=('custom:wxWindows')
depends=('lua51' 'desktop-file-utils' 'wxgtk2.8' 'webkitgtk2' )
makedepends=('cmake')
provides=('wxstedit')
conflicts=('wxstedit')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/$pkgname/$pkgname/$pkgver/wxLua-$pkgver-src.tar.gz")
md5sums=('e0a28904d046400713c07d7c4c9515ef')

prepare() {
  cd wxLua-$pkgver-src
  [ -d b ] || mkdir b

  # wxstedit doc folder fix
  sed -i 's|doc/|share/&|' modules/wxstedit/CMakeLists.txt

  # right shebang for wxluafreeze.lua
  sed -i 's| \..*/|/usr/bin/env |;1s|$|5.1|' apps/wxluafreeze/wxluafreeze.lua
}

build() {
  cd wxLua-$pkgver-src/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-2.8 \
           -DwxLua_LUA_INCLUDE_DIR=/usr/include/lua5.1 \
           -DwxLua_LUA_LIBRARY=/usr/lib/liblua.so.5.1 \
           -DwxLua_LUA_LIBRARY_USE_BUILTIN=0 \
           -DwxLua_LUA_LIBRARY_VERSION=5.1
  make
}

package() {
  cd wxLua-$pkgver-src/b
  make DESTDIR="$pkgdir/" install

  # mv lua module
  install -d "$pkgdir/usr/lib/lua/5.1"
  mv "$pkgdir/usr/lib/libwx.so" "$pkgdir/usr/lib/lua/5.1/wx.so"

  # desktop file
  install -Dm644 ../distrib/autopackage/$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # mime file
  install -Dm644 ../distrib/autopackage/$pkgname.xml \
    "$pkgdir/usr/share/mime/packages/$pkgname.xml"

  # license
  install -Dm 644 ../docs/licence.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
